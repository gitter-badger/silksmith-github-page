buildscript {
	repositories {
		jcenter()
		maven { url = "http://dl.bintray.com/silksmith-io/maven" }
		maven { url = "http://oss.jfrog.org/artifactory/libs-snapshot" }
	}
	dependencies {
		classpath group: 'io.silksmith', name: 'silksmith', version: "0.3.0-SNAPSHOT"
	}

    // use a different compiler version
    configurations.classpath.resolutionStrategy.eachDependency { details ->
        if (details.requested.group == "com.google.javascript" && details.requested.name == "closure-compiler") {
            details.useVersion "v20150126"
        }
    }
}

apply plugin: 'silksmith'
apply plugin: 'silksmith-angular'
apply plugin: 'idea'

repositories {
    jcenter()
    maven { url="http://dl.bintray.com/silksmith-io/silk"}
}
dependencies {
    web "io.silksmith.libs:closure-base:1.0.0+smith.+"
    web "io.silksmith.libs:bootstrap-sass:3.3.3+smith.+"
    web "io.silksmith.libs:jquery:1.11.2+smith.+"
}
closureCompileJS{
    entryPoint="silksmith.page"
}
server {
    dir file("www")
}

task wrapper(type: Wrapper) { gradleVersion = '2.3' }


if (project.plugins.hasPlugin("silksmith")) {
    project.idea {
        module {
            if (!scopes.COMPILE) {
                scopes.COMPILE = ["plus": []]
            }
            scopes.COMPILE.plus += [project.configurations.web]

            if (!scopes.TEST) {
                scopes.TEST = ["plus": [], "minus": []]
            }
            scopes.TEST.plus += [project.configurations.testWeb]
            scopes.TEST.minus += [project.configurations.web]
        }
    }
}
